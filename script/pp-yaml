#!perl

use 5.010;
use strict;
use warnings;

use Getopt::Long;

# AUTHORITY
# DATE
# DIST
# VERSION

my $color = $ENV{COLOR} // -t STDOUT;
#my $pretty = 1;
my $mod = '';
GetOptions(
    #'pretty!' => \$pretty,
    'color!' => \$color,
    'pp' => sub { $mod = 'pp' },
    'xs' => sub { $mod = 'xs' },
    'help' => sub {
        print "Usage: pp-yaml [OPTIONS] <file>\n";
        exit 0;
    },
);

undef $/;

my $res;
if ($mod eq 'xs') {
    require YAML::XS;
    $res = YAML::XS::Dump(YAML::XS::Load(scalar <>));
} elsif ($mod eq 'pp') {
    require YAML::Old;
    $res = YAML::Old::Dump(YAML::Old::Load(scalar <>));
} else {
    require YAML::XS;
    if ($color) {
        require YAML::Tiny::Color;
        $res = YAML::Tiny::Color::Dump(YAML::XS::Load(scalar <>));
    } else {
        $res = YAML::XS::Dump(YAML::XS::Load(scalar <>));
    }
}
print $res;
print "\n" if (-t STDOUT) && $res !~ /\R\z/;

1;
#ABSTRACT: Pretty-print (and colorize) YAML
#PODNAME:

=head1 DESCRIPTION

By default, when in interactive mode will pretty-print and colorize YAML using
L<YAML::Tiny::Color>, and when piped will pretty-print YAML using L<YAML::XS>.

=head1 OPTIONS

=over

=item * --color, --no-color

Force using color (YAML::Tiny::Color) or no color. The default is to check
whether STDOUT is not piped (interactive mode) to enable color.

=item * --pp

Force using a PP (pure-Perl) module (currently L<YAML::Old>) instead of the
default L<YAML::XS>.

=item * --xs

Force using an XS module (currently L<YAML::XS>).

=back


=head1 ENVIRONMENT

=head2 COLOR

Boolean. Set default for color.
