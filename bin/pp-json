#!perl

use 5.010;
use strict;
use warnings;

use Getopt::Long;

# VERSION

my $json_mod = '';
GetOptions(
    'pp' => sub { $json_mod = 'pp' },
    'xs' => sub { $json_mod = 'xs' },
    'help' => sub {
        print "Usage: pp-json [--pp|--xs] <file>\n";
        exit 0;
    },
);

my $json;
if ($json_mod eq 'pp') {
    require JSON::PP; $json = JSON::PP->new;
} elsif ($json_mod eq 'xs') {
    require Cpanel::JSON::XS; $json = Cpanel::JSON::XS->new;
} else {
    require JSON::MaybeXS; $json = JSON::MaybeXS->new;
}

$json->canonical->allow_nonref->pretty(1);

undef $/;
print $json->encode($json->decode(scalar <>));

1;
#ABSTRACT: Pretty-print JSON
#PODNAME:

=head1 OPTIONS

=over

=item * --pp

Force using a PP module (currently L<JSON::PP>) instead of the default
L<JSON::MaybeXS>.

=item * --xs

Force using an XS module (currently L<Cpanel::JSON::XS>) instead of
the default L<JSON::MaybeXS>.

=back
