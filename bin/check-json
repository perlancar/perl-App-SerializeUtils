#!/usr/bin/perl

use 5.010;
use strict;
use warnings;

use Getopt::Long;

# VERSION

my $json_mod = '';
GetOptions(
    'pp' => sub { $json_mod = 'PP' },
    'xs' => sub { $json_mod = 'XS' },
    'help' => sub {
        print "Usage: $0 [--pp|--xs] <file>\n";
        exit 0;
    },
);

my $json;
if ($json_mod eq 'PP') {
    require JSON::PP; $json = JSON::PP->new->allow_nonref;
} elsif ($json_mod eq 'XS') {
    require JSON::XS; $json = JSON::XS->new->allow_nonref;
} else {
    require JSON;     $json = JSON    ->new->allow_nonref;
}

eval {
    undef $/;
    $json->decode(~~<>);
};

die $@ if $@;

1;
#ABSTRACT: Check JSON syntax
#PODNAME: check-json

=head1 DESCRIPTION

Check JSON syntax. Prints nothing if JSON syntax is okay.


=head1 COMMAND-LINE OPTIONS

=over

=item * --pp

Force using L<JSON::PP> instead of L<JSON>.

=item * --xs

Force using L<JSON::XS> instead of L<JSON>.

=back

=cut
